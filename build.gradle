buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
    }
}

plugins {
    id 'java'
    id 'application'
    id "com.google.protobuf" version "0.8.12"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.12.2'
}

ext {
    descriptorDir = "$buildDir/descriptors"
    descriptorFileSuffix = ".desc"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.12.3"
    }
    generateProtoTasks {
        generateProto.doFirst {
            //Make sure we first clean all generated files each time we build
            delete fileTree(dir: "$buildDir/generated/source/proto")
        }
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.path = "$descriptorDir/${task.sourceSet.name}$descriptorFileSuffix"
        }
    }
}

application {
    //Define the main class for the application.
    mainClassName = 'tech.systemsdesign.proto.descriptors.App'
}

run {
    //Pass in the descriptor output dir and the descriptor file suffix
    args descriptorDir, descriptorFileSuffix
}
